// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-rc1'
        classpath 'com.novoda:bintray-release:0.3.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// speed up compilation when build on circle ci
project.ext.onCircleCi = "true".equals(System.getenv("CIRCLECI"))

allprojects {
    repositories {
        jcenter()
    }

    // improve build server performance by disabling incremental on circleci
    tasks.withType(JavaCompile) {
        options.incremental = !rootProject.ext.onCircleCi
    }
}

// improve build server performance by disabling preDex on circle ci
// http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = !rootProject.ext.onCircleCi
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = !rootProject.ext.onCircleCi
        }
    }
}

// also clean root build folder on clean command
task clean(type: Delete) {
    delete rootProject.buildDir
}
