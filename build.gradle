// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.3.72'
    }
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.21"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.2.1"
        classpath "org.jetbrains.kotlin:kotlin-allopen:1.4.21"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// speed up compilation when build on circle ci
project.ext.isCiEnvironment = "true".equals(System.getenv("IS_CI_ENVIRONMENT"))

allprojects {
    repositories {
        jcenter()
    }

    // improve build server performance by disabling incremental on circleci
    tasks.withType(JavaCompile) {
        options.incremental = !rootProject.ext.isCiEnvironment
    }
}

// improve build server performance by disabling preDex on circle ci
// http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.
subprojects {
    if(rootProject.ext.isCiEnvironment) project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            println("disable preDexLibraries")
            project.android.dexOptions.preDexLibraries = !rootProject.ext.isCiEnvironment
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            println("disable preDexLibraries")
            project.android.dexOptions.preDexLibraries = !rootProject.ext.isCiEnvironment
        }
    }
}

// also clean root build folder on clean command
task clean(type: Delete) {
    delete rootProject.buildDir
}
